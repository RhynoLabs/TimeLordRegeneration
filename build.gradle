plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

apply from: 'versioning.gradle'

version = getCurrentVersion()
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
    maven { url = "https://maven.fabricmc.net/" }
    mavenCentral()

	maven {
		url "https://jitpack.io"
		metadataSources {
			artifact() // Look directly for artifact
		}
	}

	// theo
	maven { url "https://theo.is-a.dev/maven/" }

	// YACL
	maven { url 'https://maven.isxander.dev/releases' }

	// AmbleKit
	maven { url "https://api.modrinth.com/maven" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// amblekit
	modCompileOnlyApi "com.github.amblelabs:ait:${project.ait_version}"
	modImplementation "com.github.amblelabs:modkit:${project.use_amblekit_dev ? project.amblekit_dev_version : project.amblekit_version}"

	// getter setter
	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'
	testCompileOnly 'org.projectlombok:lombok:1.18.34'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

	// theo
	modImplementation "dev.drtheo:multidim:${project.multidim_version}"
	modImplementation "dev.drtheo:scheduler:${project.scheduler_version}"
	modImplementation "dev.drtheo:queue:${project.queue_version}"

	// yacl
	modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"

	// other
	include(implementation "net.objecthunter:exp4j:0.4.8")
}

loom {
	// accessWidenerPath = file("src/main/resources/timelordregen.accesswidener")

	runs {
		datagen {
			inherit server
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=timelordregen"

			runDir "build/datagen"
		}
	}

	mods {
		modid {
			sourceSet sourceSets.main
		}
	}
}

// Add the generated resources to the main source set
sourceSets {
	main {
		resources {
			srcDirs += [
					"src/main/generated"
			]
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	// needed because inputs.properties.archivesName is not accessible configuration time
	def archivesNameValue = project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${archivesNameValue.get()}" }
	}
}